-- Author: Clone.

global g_CloneTools_ScriptsDir

fn AddUSD_uniqueAppend arr value =
(
	if value == undefined or value == "" do return arr
	local v = value as string
	for e in arr where (toLower (e as string)) == (toLower v) do return arr
	append arr v
	arr
)

fn AddUSD_isWritableDir dirPath =
(
	if dirPath == undefined or dirPath == "" do return false
	try (
		makeDir dirPath
		local probe = dirPath + "\\_clonetools_write_test.tmp"
		local f = createFile probe
		close f
		deleteFile probe
		true
	) catch (false)
)

fn AddUSD_detectSourceDir selfFileName =
(
	local candidates = #()
	if ::g_CloneTools_ScriptsDir != undefined and ::g_CloneTools_ScriptsDir != "" do
		AddUSD_uniqueAppend candidates ::g_CloneTools_ScriptsDir

	local pA = try(getSourceFileName())catch("")
	if pA != undefined and pA != "" do AddUSD_uniqueAppend candidates (getFilenamePath pA)

	local pB = try(sourceFileName)catch("")
	if pB != undefined and pB != "" do AddUSD_uniqueAppend candidates (getFilenamePath pB)

	local pC = try(sysInfo.currentdir)catch("")
	if pC != undefined and pC != "" do AddUSD_uniqueAppend candidates pC

	AddUSD_uniqueAppend candidates ((getDir #userScripts) + "\\CloneTools\\")

	for dir in candidates do (
		local direct = dir + selfFileName
		local fromChasers = dir + "cloneTools\\" + selfFileName
		if doesFileExist direct do return dir
		if doesFileExist fromChasers do return (dir + "cloneTools\\")
	)
	undefined
)

fn AddUSD_findSourceFile sourceDir fileName =
(
	local p1 = sourceDir + fileName
	if doesFileExist p1 do return p1
	local p2 = sourceDir + "cloneTools\\" + fileName
	if doesFileExist p2 do return p2
	""
)

fn AddUSD_copyFile sourcePath destPath =
(
	if sourcePath == undefined or sourcePath == "" do return false
	if destPath == undefined or destPath == "" do return false
	if not doesFileExist sourcePath do return false

	if (toLower sourcePath) == (toLower destPath) do return true
	if doesFileExist destPath do deleteFile destPath
	copyFile sourcePath destPath
)

fn AddUSD_installFiles selfFileName extraFiles:#() =
(
	local sourceDir = AddUSD_detectSourceDir selfFileName
	if sourceDir == undefined or sourceDir == "" do return false

	local targetDir = (getDir #userScripts) + "\\CloneTools\\"
	try(makeDir targetDir)catch()

	local files = #()
	append files selfFileName
	for f in extraFiles do append files f

	local copiedSelf = false
	for f in files do (
		local src = AddUSD_findSourceFile sourceDir f
		if src != "" then (
			local dst = targetDir + f
			if AddUSD_copyFile src dst and (toLower f) == (toLower selfFileName) do copiedSelf = true
		)
	)

	if copiedSelf do ::g_CloneTools_ScriptsDir = targetDir
	copiedSelf
)

fn AddUSD_collectMacroPaths selfFileName =
(
	local paths = #()
	local sourceDir = AddUSD_detectSourceDir selfFileName
	if sourceDir != undefined and sourceDir != "" do AddUSD_uniqueAppend paths (sourceDir + selfFileName)
	AddUSD_uniqueAppend paths ((getDir #userScripts) + "\\CloneTools\\" + selfFileName)
	paths
)

fn AddUSD_collectStartupDirs =
(
	local dirs = #()
	local d1 = try(getDir #userStartupScripts)catch("")
	if d1 != undefined and d1 != "" do AddUSD_uniqueAppend dirs d1
	AddUSD_uniqueAppend dirs ((getDir #userScripts) + "\\startup")
	local d2 = try(getDir #startupScripts)catch("")
	if d2 != undefined and d2 != "" do AddUSD_uniqueAppend dirs d2
	dirs
)

fn AddUSD_escapePath path = (substituteString path "\\" "\\\\")

fn AddUSD_installUserMacroMirror selfFileName mirrorName =
(
	local sourceDir = AddUSD_detectSourceDir selfFileName
	if sourceDir == undefined or sourceDir == "" do return false
	local sourceFile = AddUSD_findSourceFile sourceDir selfFileName
	if sourceFile == "" do return false

	local userMacrosDir = getDir #userMacros
	if userMacrosDir == undefined or userMacrosDir == "" do return false
	if not AddUSD_isWritableDir userMacrosDir do return false

	local mirrorPath = userMacrosDir + "\\" + mirrorName
	if doesFileExist mirrorPath do deleteFile mirrorPath
	AddUSD_copyFile sourceFile mirrorPath
)

fn PowerUSD_getScriptsDir =
(
	if ::g_CloneTools_ScriptsDir != undefined and ::g_CloneTools_ScriptsDir != "" then
		::g_CloneTools_ScriptsDir
	else
		((getDir #userScripts) + "\\CloneTools\\")
)

fn AddUSDProps_InstallStartupLoader =
(
	local selfFile = "addusdproperties.ms"
	local deps = #("Clone_USD_AttributeDef.ms")
	AddUSD_installFiles selfFile extraFiles:deps
	AddUSD_installUserMacroMirror selfFile "AddUSDProperties_CloneTools.mcr"

	local macroPaths = AddUSD_collectMacroPaths selfFile
	local macroList = ""
	for i = 1 to macroPaths.count do (
		local comma = if i < macroPaths.count then "," else ""
		macroList += ("\t\t\"" + (AddUSD_escapePath macroPaths[i]) + "\"" + comma + "\n")
	)

	local loaderScript =
		"-- Auto-generated by AddUSDProperties\n" +
		"(\n" +
		"\tlocal macroPaths = #(\n" +
		macroList +
		"\t)\n" +
		"\tfor p in macroPaths do (\n" +
		"\t\tif doesFileExist p do (\n" +
		"\t\t\tfileIn p quiet:true\n" +
		"\t\t\texit\n" +
		"\t\t)\n" +
		"\t)\n" +
		")\n"

	local installCount = 0
	for startupDir in AddUSD_collectStartupDirs() do (
		if AddUSD_isWritableDir startupDir then (
			local loaderPath = startupDir + "\\AddUSDProps_LoadMacros.ms"
			try (
				local f = createFile loaderPath
				format "%" loaderScript to:f
				close f
				installCount += 1
			) catch ()
		)
	)

	installCount > 0
)

macroScript AddUSDProperties
category:"CloneTools"
toolTip:"Add USD Properties to selected objects"
buttonText:"Add USD Properties"
(
	if selection.count == 0 then
	(
		messageBox "No objects selected."
		return false
	)

	local attrDefPath = PowerUSD_getScriptsDir() + "Clone_USD_AttributeDef.ms"
	if doesFileExist attrDefPath then
	(
		fileIn attrDefPath
		for obj in selection do ::addUSDProperties obj
		format "Added USD Properties to % object(s)\n" selection.count
	)
	else messageBox ("Clone_USD_AttributeDef.ms not found.\n\nLooked in:\n" + attrDefPath)
)

(
	local selfPath = getSourceFileName()
	if selfPath != undefined and selfPath != "" do
		::g_CloneTools_ScriptsDir = getFilenamePath selfPath
)

AddUSDProps_InstallStartupLoader()
