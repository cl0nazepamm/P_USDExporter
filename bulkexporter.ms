-- Author: Clone.

global g_CloneTools_ScriptsDir

fn PowerUSD_getScriptsDir =
(
	if g_CloneTools_ScriptsDir != undefined and g_CloneTools_ScriptsDir != "" then
		g_CloneTools_ScriptsDir
	else
		((getDir #userScripts) + "/CloneTools/")
)

fn BulkUSD_escapePath path = (substituteString path "\\" "\\\\")

fn BulkUSD_isWritableDir dirPath =
(
	if dirPath == undefined or dirPath == "" do return false
	try (
		makeDir dirPath
		local probe = dirPath + "\\_clonetools_write_test.tmp"
		local f = createFile probe
		close f
		deleteFile probe
		true
	) catch (false)
)

fn BulkUSD_InstallStartupLoader =
(
	local selfFile = "bulkexporter.ms"

	local sourceFile = try(getSourceFileName())catch("")
	if sourceFile == undefined or sourceFile == "" do return false
	local sourceDir = getFilenamePath sourceFile

	local targetDir = (getDir #userScripts) + "\\CloneTools\\"
	try(makeDir targetDir)catch()
	local destPath = targetDir + selfFile
	if (toLower sourceFile) != (toLower destPath) do (
		if doesFileExist destPath do deleteFile destPath
		copyFile sourceFile destPath
	)

	local userMacrosDir = getDir #userMacros
	if userMacrosDir != undefined and userMacrosDir != "" and BulkUSD_isWritableDir userMacrosDir do (
		local mcrPath = userMacrosDir + "\\BulkUSDExport_CloneTools.mcr"
		if doesFileExist mcrPath do deleteFile mcrPath
		copyFile sourceFile mcrPath
	)

	local startupDirs = #()
	local d1 = try(getDir #userStartupScripts)catch("")
	if d1 != undefined and d1 != "" do append startupDirs d1
	append startupDirs ((getDir #userScripts) + "\\startup")
	local d2 = try(getDir #startupScripts)catch("")
	if d2 != undefined and d2 != "" do append startupDirs d2

	local escapedDest = BulkUSD_escapePath destPath
	local loaderScript =
		"-- Auto-generated by BulkUSD\n" +
		"(\n" +
		"\tlocal macrosPath = \"" + escapedDest + "\"\n" +
		"\tif doesFileExist macrosPath do fileIn macrosPath quiet:true\n" +
		")\n"

	for startupDir in startupDirs do (
		if BulkUSD_isWritableDir startupDir do (
			local loaderPath = startupDir + "\\BulkUSD_LoadMacros.ms"
			try (
				local f = createFile loaderPath
				format "%" loaderScript to:f
				close f
			) catch ()
		)
	)

	true
)

macroScript PowerUSDBulkExport
category:"CloneTools"
toolTip:"Bulk Export Selected to USD"
buttonText:"Clone Bulk USD"
(
	persistent global g_PowerUSD_lastDir

	rollout rBulkUSD "Bulk USD Exporter" width:240
	(
		bitmap bmp_logo "Logo" fileName:"" height:50 width:220 align:#center

		group "Grouping"
		(
			checkbox chk_layers "By Layers" checked:false tooltip:"One USD file per Layer."
			checkbox chk_hierarchy "By Hierarchies" checked:true tooltip:"One USD file per Hierarchy Root."
		)

		group "Options"
		(
			checkbox chk_origin "Move to Origin" checked:false tooltip:"Temporarily moves root to 0,0,0 for export."
			checkbox chk_thumbnail "Generate Thumbnails" checked:false tooltip:"Render a thumbnail for each exported asset."
		)

		button btn_export "EXPORT SELECTED" width:200 height:40 highlightColor:(color 0 200 0)
		button btn_export_sets "EXPORT SELECTION SETS" width:200 height:30

		on chk_layers changed state do
			if state do chk_hierarchy.checked = false

		on chk_hierarchy changed state do
			if state do chk_layers.checked = false

		on rBulkUSD open do
		(
			local iconsDir = GetDir #userIcons
			local logoPath = iconsDir + "\\PowerUSD\\powerusd_logo.png"
			if doesFileExist logoPath do bmp_logo.fileName = logoPath
		)

		on btn_export_sets pressed do
		(
			fileIn (PowerUSD_getScriptsDir() + "Clone_USD_HierarchyBuilder.ms")
			if selectionSets.count == 0 then (messageBox "No Selection Sets found."; return false)

			if g_PowerUSD_lastDir == undefined do g_PowerUSD_lastDir = (getDir #export)
			local outDir = getSavePath caption:"Select Output Directory" initialDir:g_PowerUSD_lastDir
			if outDir == undefined do return false
			g_PowerUSD_lastDir = outDir

			local scriptsDir = PowerUSD_getScriptsDir()
			if doesFileExist (scriptsDir + "Clone_USD_CleanStruct.py") do python.ExecuteFile (scriptsDir + "Clone_USD_CleanStruct.py")
			if doesFileExist (scriptsDir + "Clone_USD_usdWrapper.py") do python.ExecuteFile (scriptsDir + "Clone_USD_usdWrapper.py")
			if doesFileExist (scriptsDir + "Clone_USD_PropertiesChaser.py") do python.ExecuteFile (scriptsDir + "Clone_USD_PropertiesChaser.py")

			local exportSets = #()
			for i = 1 to selectionSets.count do
			(
				local setName = getNamedSelSetName i
				local setNodes = for obj in selectionSets[i] collect obj
				if setNodes.count > 0 do
					append exportSets (::ExportNodeData exportName:setName rootNodes:setNodes moveRoot:undefined subFolder:undefined parentName:undefined)
			)
			exportSets = ::partitionCamerasToSingleExport exportSets cameraFileName:"Cameras"
			exportSets = ::bundleCameraTargetsInExportSets exportSets

			local currentSel = selection as array
			local startF = animationRange.start.frame as integer
			local endF = animationRange.end.frame as integer
			local fps = frameRate as integer
			local exportSuccess = ::performBatchExport exportSets outDir chk_origin.checked chk_thumbnail.checked startF endF fps

			if exportSuccess do
			(
				select currentSel
				format "\n=== Bulk Export Complete ===\n"
			)
		)

		on btn_export pressed do
		(
			fileIn (PowerUSD_getScriptsDir() + "Clone_USD_HierarchyBuilder.ms")
			local rawSelection = selection as array
			if rawSelection.count == 0 then (messageBox "No objects selected."; return false)

			if g_PowerUSD_lastDir == undefined do g_PowerUSD_lastDir = (getDir #export)
			local outDir = getSavePath caption:"Select Output Directory" initialDir:g_PowerUSD_lastDir
			if outDir == undefined do return false
			g_PowerUSD_lastDir = outDir

			local scriptsDir = PowerUSD_getScriptsDir()
			if doesFileExist (scriptsDir + "Clone_USD_CleanStruct.py") do python.ExecuteFile (scriptsDir + "Clone_USD_CleanStruct.py")
			if doesFileExist (scriptsDir + "Clone_USD_usdWrapper.py") do python.ExecuteFile (scriptsDir + "Clone_USD_usdWrapper.py")
			if doesFileExist (scriptsDir + "Clone_USD_PropertiesChaser.py") do python.ExecuteFile (scriptsDir + "Clone_USD_PropertiesChaser.py")

			local exportSets = #()

			-- LAYERS: all objects on layer exported together
			if chk_layers.checked then
			(
				local layerNames = #()

				for obj in rawSelection do
				(
					local lName = obj.layer.name
					if findItem layerNames lName == 0 do append layerNames lName
				)

				for lName in layerNames do
				(
					local layer = LayerManager.getLayerFromName lName
					local layerNodes = #()
					layer.nodes &layerNodes

					append exportSets (::ExportNodeData exportName:lName rootNodes:layerNodes moveRoot:undefined subFolder:undefined parentName:undefined)
				)
			)
			-- HIERARCHIES: entire hierarchy exported together
			else if chk_hierarchy.checked then
			(
				local processedRoots = #()

				for obj in rawSelection do
				(
					local root = ::getHierarchyRoot obj
					if findItem processedRoots root == 0 do
					(
						append processedRoots root
						local fullTree = ::getAllDescendants root

						append exportSets (::ExportNodeData exportName:root.name rootNodes:fullTree moveRoot:root subFolder:undefined parentName:undefined)
					)
				)
			)
			-- STANDARD: individual objects/groups, instances exported together
			else
			(
				local groupsProcessed = #()
				local geometryObjects = #()
				local groupObjects = #()

				for obj in rawSelection do
				(
					if isGroupMember obj then
					(
						local grpHead = obj
						while (isGroupMember grpHead) do grpHead = grpHead.parent
						if findItem groupsProcessed grpHead == 0 do
						(
							append groupsProcessed grpHead
							append groupObjects grpHead
						)
					)
					else if isGroupHead obj then
					(
						if findItem groupsProcessed obj == 0 do
						(
							append groupsProcessed obj
							append groupObjects obj
						)
					)
					else
					(
						append geometryObjects obj
					)
				)

				for grp in groupObjects do
					append exportSets (::ExportNodeData exportName:grp.name rootNodes:#(grp) moveRoot:grp subFolder:undefined parentName:undefined)

				local instanceGroups = ::groupByInstances geometryObjects

				for group in instanceGroups do
				(
					local master = ::selectMasterInstance group
					append exportSets (::ExportNodeData exportName:master.name rootNodes:group moveRoot:master subFolder:undefined parentName:undefined)
				)
			)

			exportSets = ::partitionCamerasToSingleExport exportSets cameraFileName:"Cameras"
			exportSets = ::bundleCameraTargetsInExportSets exportSets

			local currentSel = selection as array
			local startF = animationRange.start.frame as integer
			local endF = animationRange.end.frame as integer
			local fps = frameRate as integer
			local exportSuccess = ::performBatchExport exportSets outDir chk_origin.checked chk_thumbnail.checked startF endF fps

			if exportSuccess do
			(
				select rawSelection
				format "\n=== Bulk Export Complete ===\n"
			)
		)
	)

	createDialog rBulkUSD
)

(
	local selfPath = getSourceFileName()
	if selfPath != undefined and selfPath != "" do
		g_CloneTools_ScriptsDir = getFilenamePath selfPath
)

BulkUSD_InstallStartupLoader()
